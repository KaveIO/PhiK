CMAKE_MINIMUM_REQUIRED(VERSION 3.14)

PROJECT(
    _phik_simulation
    VERSION ${PHIK_VERSION_INFO}
    DESCRIPTION "C++ bindings for simulation RXC tables"
    LANGUAGES CXX
)
SET(CMAKE_CXX_STANDARD 14)
# Define CMAKE_INSTALL_xxx: LIBDIR, INCLUDEDIR
include(GNUInstallDirs)

FIND_PACKAGE(pybind11 CONFIG REQUIRED)
INCLUDE(${PROJECT_SOURCE_DIR}/phik/simcore/CMakeLists.txt)

SET(DEFAULT_BUILD_TYPE "Release")
IF (NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
  MESSAGE(STATUS "Setting build type to '${DEFAULT_BUILD_TYPE}' as none was specified.")
  SET(CMAKE_BUILD_TYPE "${DEFAULT_BUILD_TYPE}" CACHE STRING "Choose the type of build." FORCE)
  # Set the possible values of build type for cmake-gui
  SET_PROPERTY(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS
    "Debug" "Release" "Asan" "MinSizeRel" "RelWithDebInfo")
ENDIF ()

IF (PHIK_BUILDING_WHEELS)
    # disable architecture flags
    SET(PHIK_RELEASE_FLAGS "")
    MESSAGE(STATUS "phik: Building for non-native host")
ELSE ()
    SET(PHIK_RELEASE_FLAGS "")
    INCLUDE(CheckCXXCompilerFlag)
    CHECK_CXX_COMPILER_FLAG(-march=native COMPILER_SUPPORTS_MARCH_NATIVE)
    IF(COMPILER_SUPPORTS_MARCH_NATIVE)
        SET(PHIK_RELEASE_FLAGS ${PHIK_RELEASE_FLAGS} -march=native)
    ENDIF()
    CHECK_CXX_COMPILER_FLAG(-mtune=native COMPILER_SUPPORTS_MTUNE_NATIVE)
    IF(COMPILER_SUPPORTS_MTUNE_NATIVE)
        SET(PHIK_RELEASE_FLAGS ${PHIK_RELEASE_FLAGS} -mtune=native)
    ENDIF()
    CHECK_CXX_COMPILER_FLAG(-ftree-vectorize COMPILER_SUPPORTS_FTREE)
    IF(COMPILER_SUPPORTS_FTREE)
        SET(PHIK_RELEASE_FLAGS ${PHIK_RELEASE_FLAGS} -ftree-vectorize)
    ENDIF()
    CHECK_CXX_COMPILER_FLAG(-mavx COMPILER_SUPPORTS_MAVX)
    IF(COMPILER_SUPPORTS_MAVX2)
        SET(PHIK_RELEASE_FLAGS ${PHIK_RELEASE_FLAGS} -mavx)
    ENDIF()
    CHECK_CXX_COMPILER_FLAG(-mavx2 COMPILER_SUPPORTS_MAVX2)
    IF(COMPILER_SUPPORTS_MAVX2)
        SET(PHIK_RELEASE_FLAGS ${PHIK_RELEASE_FLAGS} -mavx2)
    ENDIF()
ENDIF()

# Quiet a warning, since this project is only valid with SKBUILD
SET(ignoreMe "${SKBUILD}")
